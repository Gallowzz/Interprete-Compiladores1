program     → statement*
statement   → varDecl
            | assignment
            | ifStmt
            | whileStmt
            | printStmt
            | block
varDecl     → "int" IDENTIFIER ("=" expression)? ";"
assignment  → IDENTIFIER "=" expression ";"
ifStmt      → "if" "(" expression ")" statement ("else" statement)?
whileStmt   → "while" "(" expression ")" statement
printStmt   → "print" "(" expression ")" ";"
block       → "{" statement* "}"

expression  → logicalOr
logicalOr   → logicalAnd ("||" logicalAnd)*
logicalAnd  → equality ("&&" equality)*
equality    → comparison (("==" | "!=") comparison)*
comparison  → term (("<" | ">" | "<=" | ">=") term)*
term        → factor (("+" | "-") factor)*
factor      → unary (("*" | "/" | "%") unary)*
unary       → ("!" | "-") unary
            | primary
primary     → INTEGER
            | IDENTIFIER
            | "(" expression ")"
